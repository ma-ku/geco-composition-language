grammar de.cau.cs.se.geco.architecture.Architecture with org.eclipse.xtext.xbase.Xbase

generate architecture "http://www.cau.de/cs/se/geco/architecture/Architecture"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'package' name=QualifiedName
	importSection=XImportSection?
	registeredPackages+=RegisteredPackage+
	
	// prerequisites
	// handle results
	(conections+=Connection | metamodels+=MetamodelSequence)*
;

MetamodelSequence:
	'model' type=ModelNodeType metamodels+=Metamodel (',' metamodels+=Metamodel)*
;

Metamodel:
	name=ValidID 
;

RegisteredPackage:
	'register' name=ValidID (modelPackage=[ecore::EPackage|STRING] |(isText?='text' extension=STRING))
;

Connection:
	Generator | Weaver
;

Weaver:
	'weave' weaver=JvmTypeReference  
		sourceModel=SourceModelNodeSelector
		aspectModel=AspectModel
		('=>' targetModel=TargetModelNodeType)?
;

AspectModel:
	(':' TargetModelNodeType) | Generator
;
	
Generator:
	'generate' generator=JvmTypeReference 
		sourceModel=SourceModelNodeSelector
		targetModel=TargetModelNodeType
		('->' writeTraceModel=TraceModel)?
		('<-' readTraceModels+=[TraceModel|ValidID] (',' readTraceModels+=[TraceModel|ValidID])*)?
;

SourceModelNodeSelector: 
	reference = [Metamodel|ValidID] ('/' property=NodeProperty)? | 
	{SourceModelNodeSelector} 'null' 
;

TargetModelNodeType: {TargetModelNodeType}
	(reference = [Metamodel|ValidID])? (multiply?='*')?
;

ModelNodeType:
	target=[RegisteredPackage|ValidID] '/' type=[ecore::EClass|ValidID] ('/' property=NodeProperty)?
;

NodeProperty:
	property=[ecore::EReference|ValidID] ('[' constraint=ConstraintExpression ']')? ('/' subProperty=NodeProperty)? 
;

ConstraintExpression:
	CompareExpression (=>({ConstraintExpression.left=current} operator=LogicOperator) right=ConstraintExpression)?
;

CompareExpression returns ConstraintExpression:
	BasicConstraint (=>({ConstraintExpression.left=current} operator=Comparator) right=BasicConstraint)?
;

BasicConstraint returns ConstraintExpression:
	ParenthesisConstraint |
	Operand
;

ParenthesisConstraint returns ConstraintExpression:
	'(' constraint=ConstraintExpression ')'
;

Operand:
	Literal | NodeProperty
;

TraceModel:
	name=ValidID '<' nodeSetRelations+=NodeSetRelation+ '>'
;

NodeSetRelation:
	'(' 
		sourceNodes+=NodeType (',' sourceNodes+=NodeType)*
	':' 
		targetNodes+=NodeType (',' targetNodes+=NodeType)* 
	')'
;

NodeType: eclass=[ecore::EClass|ValidID];

// -----------------------------------
// Literal

Literal:
	StringLiteral | IntLiteral | FloatLiteral | BooleanLiteral | ArrayLiteral
;

ArrayLiteral:
	'{' literals+=Literal (',' literals+=Literal)* '}'
;

StringLiteral:
	value=STRING
;

IntLiteral:
	value=AINT
;

FloatLiteral:
	value=FLOAT
;

BooleanLiteral: 
	value=XBooleanLiteral
;

// -----------------------------------
// Terminals

LogicOperator:
	AND = '&' |
	OR = '|'
;

Comparator:
	EQ = '==' |
	NE = '!=' |
	GR = '>' |
	LW = '<' |
	GE = '>=' |
	LE = '<=' |
	LIKE = '~'
;

// terminals
terminal fragment DIGIT :
    '0'..'9';
    
// redefine INT terminal to allow negative numbers
terminal AINT returns ecore::EInt:
    '-'? DIGIT+;
    
// make sure the Float rule does not shadow the INT rule
terminal FLOAT returns ecore::EFloatObject :
    '-'? DIGIT+ ('.' DIGIT*) (("e"|"E") ("+"|"-")? DIGIT+)? 'f'? |
    '-'? DIGIT+ 'f';
    
