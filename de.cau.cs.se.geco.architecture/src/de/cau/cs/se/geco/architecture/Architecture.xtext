grammar de.cau.cs.se.geco.architecture.Architecture with org.eclipse.xtext.common.Terminals

generate architecture "http://www.cau.de/cs/se/geco/architecture/Architecture"
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Model:
	'package' name=QualifiedName
	'import' imports+=Import*
	registeredPackages+=RegisteredPackage+
	
	// prerequisites
	// handle results
	(connections+=Connection | metamodels+=MetamodelSequence)*
;

Import:
	WeaverImport | GeneratorImport
;

WeaverImport:
	'weave' importedNamespace = [types::JvmType|QualifiedName]
;

GeneratorImport:
	'gen' importedNamespace = [types::JvmType|QualifiedName]
;

MetamodelSequence:
	'model' type=ModelNodeType metamodels+=Metamodel (',' metamodels+=Metamodel)*
;

Metamodel:
	name=ID 
;

RegisteredPackage:
	'register' name=ID (registeredPackage = [types::JvmType|QualifiedName] |(isText?='text' extension=STRING))
;

Connection:
	Generator | Weaver
;

Weaver:
	'weave' weaver=[types::JvmType|ID]  
		sourceModel=SourceModelNodeSelector
		aspectModel=AspectModel
		('=>' targetModel=TargetModelNodeType)?
;

AspectModel:
	(':' TargetModelNodeType) | Generator
;
	
Generator:
	'generate' generator=[types::JvmType|ID] 
		sourceModel=SourceModelNodeSelector
		targetModel=TargetModelNodeType
		('->' writeTraceModel=TraceModel)?
		('<-' readTraceModels+=[TraceModel|ID] (',' readTraceModels+=[TraceModel|ID])*)?
;

SourceModelNodeSelector: 
	reference = [Metamodel|ID] ('/' property=NodeProperty)? | 
	{SourceModelNodeSelector} 'null' 
;

TargetModelNodeType: {TargetModelNodeType}
	(reference = [Metamodel|ID])? (multiply?='*')?
;

ModelNodeType:
	target=[RegisteredPackage|ID] ('/' property=NodeProperty)?
;

NodeProperty:
	property=[types::JvmMember|ID] ('[' constraint=ConstraintExpression ']')? ('/' subProperty=NodeProperty)? 
;

ConstraintExpression:
	CompareExpression (=>({ConstraintExpression.left=current} operator=LogicOperator) right=ConstraintExpression)?
;

CompareExpression returns ConstraintExpression:
	BasicConstraint (=>({ConstraintExpression.left=current} operator=Comparator) right=BasicConstraint)?
;

BasicConstraint returns ConstraintExpression:
	ParenthesisConstraint |
	Operand
;

ParenthesisConstraint returns ConstraintExpression:
	'(' constraint=ConstraintExpression ')'
;

Operand:
	Literal | NodeProperty
;

TraceModel:
	name=ID '<' nodeSetRelations+=NodeSetRelation+ '>'
;

NodeSetRelation:
	'(' 
		sourceNodes+=NodeType (',' sourceNodes+=NodeType)*
	':' 
		targetNodes+=NodeType (',' targetNodes+=NodeType)* 
	')'
;

NodeType: eclass=ID;

// -----------------------------------
// Literal

Literal:
	StringLiteral | IntLiteral | FloatLiteral | BooleanLiteral | ArrayLiteral
;

ArrayLiteral:
	'{' literals+=Literal (',' literals+=Literal)* '}'
;

StringLiteral:
	value=STRING
;

IntLiteral:
	value=INT
;

FloatLiteral:
	value=FLOAT
;

BooleanLiteral: 
	value=BOOLEAN
;

// -----------------------------------
// Terminals

LogicOperator:
	AND = '&' |
	OR = '|'
;

Comparator:
	EQ = '==' |
	NE = '!=' |
	GR = '>' |
	LW = '<' |
	GE = '>=' |
	LE = '<=' |
	LIKE = '~'
;

QualifiedName:
  ID (=>'.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?
;

// terminals
terminal fragment DIGIT :
    '0'..'9';
   
// make sure the Float rule does not shadow the INT rule
terminal FLOAT:
    '-'? DIGIT+ ('.' DIGIT*) (("e"|"E") ("+"|"-")? DIGIT+)? 'f'? |
    '-'? DIGIT+ 'f';
    
// introduce boolean values
terminal BOOLEAN:
    'true' | 'false';