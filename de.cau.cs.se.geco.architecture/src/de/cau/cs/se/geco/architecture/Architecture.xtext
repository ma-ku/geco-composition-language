grammar de.cau.cs.se.geco.architecture.Architecture with org.eclipse.xtext.xbase.Xbase

generate architecture "http://www.cau.de/cs/se/geco/architecture/Architecture"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	'package' name=QualifiedName
	importSection=XImportSection?
	metamodels+=Metamodel+ 
	// prerequisites
	// handle results
	connections+=Connection*
;

Metamodel:
	'metamodel' name=ValidID (modelPackage=[ecore::EPackage|STRING] |(isText?='text' extension=STRING))
;

Connection:
	Generator | Weaver
;

Weaver:
	'weave' merger=JvmTypeReference name=ID 
		sourceModel=SourceModelNodeSelector
		aspectModel=TargetModelNodeType
		targetModel=TargetModelNodeType
;
	
Generator:
	'generate' generator=JvmTypeReference name=ID 
		sourceModel=SourceModelNodeSelector
		targetModel=TargetModelNodeType
		('->' writeTraceModel=TraceModel)?
		('<-' readTraceModels+=[TraceModel|ValidID] (',' readTraceModels+=[TraceModel|ValidID])*)
;

SourceModelNodeSelector: 
	type=ModelNodeType | 
	{SourceModelNodeSelector} 'null' 
;

TargetModelNodeType:
	nodeType=ModelNodeType (multiply?='*')?
;

ModelNodeType:
	target=[Metamodel|ValidID] '/' type=[ecore::EClass|ValidID]
;

TraceModel:
	name=ValidID '<' nodeSetRelations+=NodeSetRelation+ '>'
;

NodeSetRelation:
	'(' 
		sourceNodes+=NodeType (',' sourceNodes+=NodeType)*
	':' 
		targetNodes+=NodeType (',' targetNodes+=NodeType)* 
	')'
;

NodeType: eclass=[ecore::EClass|ValidID];
